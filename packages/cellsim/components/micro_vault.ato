import ElectricPower, I2C, ResistorVoltageDivider
from "resistors.ato" import I2CPullup
from "texas_instruments_ref5050aidr.ato" import Texas_Instruments_REF5050AIDR
from "texas_instruments_ads1219ipwr.ato" import Texas_Instruments_ADS1219IPWR
from "texas_instruments_tmp117aidrvr.ato" import Texas_Instruments_TMP117AIDRVR
from "heater_controller.ato" import HeaterController

from "atopile/st-ldk220/ldk220.ato" import LDK220M_R


module MicroVault:
    # Interfaces
    power_9v = new ElectricPower
    power_3v3 = new ElectricPower
    i2c = new I2C

    power_9v.required = True
    power_3v3.required = True
    i2c.required = True

    # Internal rail
    _power_5v3 = new ElectricPower
    _reference_5v = new ElectricPower

    # Components
    reference = new Texas_Instruments_REF5050AIDR
    reference_regulator = new LDK220M_R
    adc = new Texas_Instruments_ADS1219IPWR
    temp_sensor = new Texas_Instruments_TMP117AIDRVR
    heater_controller = new HeaterController
    heater_vdiv = new ResistorVoltageDivider

    # I2C
    i2c ~ adc.i2c
    i2c ~ temp_sensor.i2c

    # Addresses
    assert adc.i2c.address is 0x41
    assert temp_sensor.i2c.address is 0x49

    # Power
    power_9v ~ reference_regulator.power_in
    reference_regulator.power_out ~ _power_5v3
    _power_5v3 ~ adc.power
    _power_5v3 ~ reference.power_in
    _reference_5v ~ reference.power_out
    power_3v3 ~ temp_sensor.power
    power_3v3 ~ heater_controller.power_3v3
    power_3v3.gnd ~ power_9v.gnd
    adc.reference ~ _reference_5v

    # Configure regulator
    # assert reference_regulator.v_in is 9V +/- 10%
    # assert reference_regulator.v_out within 5.3V +/- 3%

    # Set internal regulator values
    reference_regulator.feedback_div.r_bottom.resistance = 9.9kohm to 10.1kohm
    reference_regulator.feedback_div.r_top.resistance = 35.5kohm to 36.4kohm

    # Heater
    power_9v ~ heater_controller.power
    _power_5v3 ~ heater_vdiv.power
    heater_vdiv.output ~ heater_controller.reference_input
    reference.temp_out ~ heater_controller.temp_input
    # target temp to 55C
    assert heater_vdiv.r_bottom.resistance within 12kohm +/- 1%
    assert heater_vdiv.r_top.resistance within 88kohm +/- 1%

    heater_vdiv.r_top.package = "R0402"
    heater_vdiv.r_bottom.package = "R0402"
