#pragma experiment("BRIDGE_CONNECT")

import Resistor
import ElectricSignal
import Button


component ButtonSKRPACE010 from Button:
    """
    Small SMD button
    """
    lcsc_id = "C139797"
    # Pin mapping
    unnamed[0] ~ pin 1
    unnamed[0] ~ pin 2
    unnamed[1] ~ pin 3
    unnamed[1] ~ pin 4

component ButtonSKTDLDE010 from Button:
    """
    Small SMD right angle button
    """
    lcsc_id = "C115365"
    # Pin mapping
    unnamed[0] ~ pin 1
    unnamed[1] ~ pin 2

module ButtonPullup:
    """
    Connects button output to output reference.hv via a resistor when button is pressed
    Usage: Must select a button using the replace operator, e.g.
    `btn_pullup_vertical.btn -> ButtonSKRPACE010`
    """
    btn = new Button
    output = new ElectricSignal
    pullup = new Resistor

    # Connections
    output.line ~ btn.unnamed[0]
    btn.unnamed[1] ~> pullup ~> output.reference.hv

    # Configure pullup
    pullup.resistance = 10kohms +/- 20%
    pullup.package = "R0402"


module ButtonPulldown:
    """
    Connects button output to output reference.gnd via a resistor when button is pressed
    Usage: Must select a button using the replace operator, e.g.
    `btn_pulldown_vertical.btn -> ButtonSKRPACE010`
    """
    btn = new Button
    output = new ElectricSignal
    pulldown = new Resistor

    # Connections
    output.line ~ btn.unnamed[0]
    btn.unnamed[1] ~> pulldown ~> output.reference.gnd

    # Configure pulldown
    pulldown.resistance = 10kohms +/- 20%
    pulldown.package = "R0402"


module Test:
    btn_pullup_vertical = new ButtonPullup
    btn_pullup_vertical.btn -> ButtonSKRPACE010

    btn_pullup_horizontal = new ButtonPullup
    btn_pullup_horizontal.btn -> ButtonSKTDLDE010

    btn_pulldown_vertical = new ButtonPulldown
    btn_pulldown_vertical.btn -> ButtonSKRPACE010

    btn_pulldown_horizontal = new ButtonPulldown
    btn_pulldown_horizontal.btn -> ButtonSKTDLDE010
